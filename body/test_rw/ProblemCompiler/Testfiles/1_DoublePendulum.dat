/* Double Pendulum */
Real dim = 6;
Real eps = 0;

Real g  = 9.80665;
Real m1 = 0.90808;
Real m2 = 0.50965;
Real s1 = 0.273;
Real s2 = 0.223;
Real J1 = 0.0238;
Real J2 = 0.01854;
/*Real gamma1 in [0.01-eps,0.01+eps];*/
/*Real gamma2 in [0.01-eps,0.01+eps];*/
Real gamma1 = 0.01;
Real gamma2 = 0.01;
Real l1 = 0.419;
Real l2 = 0.480;
Interval X[dim];
Interval U = [-2,2];
X[0] = [0,2*Pi];
X[1] = [0,2*Pi];
X[2] = [-Pi,Pi];
X[3] = [-Pi,Pi];
X[4] = [-1.7,1.7];
X[5] = [-1.3,1.3];

f: (x,u) in (Real[dim],U) to y[dim]
{
	Real M[2][2];	
	M[0][0] = m2*l1^2 + m1*s1^2+J1    ;
	M[0][1] = cos(x[0]-x[1])*l1*m2*s2 ;
	M[1][0] = M[0][1]                 ;
	M[1][1] = m2*s2^2+J2              ;
	Real Q[2][2];
	Q[0][0] = 0 ;	
	Q[0][1] = -sin(x[0]-x[1])*l1*m2*s2;
	Q[1][0] = -Q[0][1]                ;
	Q[1][1] = 0                       ;
	Real R[2][2]                      ;
	R[0][0] = -gamma1 - gamma2        ;
	R[0][1] = gamma2                  ;
	R[1][0] = gamma2                  ;
	R[1][1] = -gamma2                 ;
	Real B[2];
	B[0] = -(l1*m2+m1*s1)*(g*sin(x[0])+cos(x[0])*u);
	B[1] = -m2*s2*(g*sin(x[1])+cos(x[1])*u);
	Real detM ;
	detM =  (J1+(l1^2)*m2+m1*s1^2)*(J2+m2*s2^2)-(l1^2)*(m2^2)*(s2^2)*cos(x[0]-x[1])^2;
	Real MInv[2][2];
	MInv[0][0] = (J2 + m2*s2^2) / detM;
	MInv[0][1] = -l1*m2*s2*cos(x[0]-x[1])/ detM;
	MInv[1][0] = -l1*m2*s2*cos(x[0]-x[1])/ detM;
	MInv[1][1] = (J1+(l1^2)*m2+m1*s1^2)/ detM;
	Real q[2];
	for(i=0,1,+)
	{
		q[i] = B[i];
		for(j=0,1,+)
		{
			q[i] = q[i] + Q[i][j]*(x[j+2]^2) ;
		}
		for(j=0,1,+)
		{
			q[i] = q[i] + R[i][j]*x[j+2] ;
		}
	}
	Real qq[2];
	for(i=0,1,+)
	{
		qq[i] = 0;
		for(j=0,1,+)
		{	
			qq[i] = qq[i] + MInv[i][j]*q[j];
		}
	}
	y[0] = x[2];
	y[1] = x[3];
	y[2] = qq[0];
	y[3] = qq[1];
	y[4] = x[5];
	y[5] = u;
}

Function : (t,x0,u) in ([0,0.01],X,U) to x[dim]
{
	initialvalue(x,x0);
	diff(x,t) = f(x,u);
}

